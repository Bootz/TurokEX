////////////////////////////////////////////////////
//
// Vars
//
////////////////////////////////////////////////////

TurokAmbienceJungle     ambJungle;
TurokAmbienceLostWorld  ambLostWorld;
TurokAmbienceRuins      ambRuins;
TurokAmbienceSwamp      ambSwamp;
TurokAmbienceValley     ambValley;
TurokAmbienceVillage    ambVillage;
TurokAmbienceCatcomb01  ambCatcomb01;
TurokAmbienceCatcomb02  ambCatcomb02;
TurokAmbienceCave       ambCave;

////////////////////////////////////////////////////
//
// TurokArea
//
////////////////////////////////////////////////////

class TurokArea : AreaComponent {
    kArea @self;
    int ambienceType;
    bool bSky;
    float skyYPlane;
    kActor @skyactor1;
    
    //
    // TurokArea::TurokArea
    //
    
    TurokArea(kArea @area) {
        @self           = area;
        ambienceType    = 0;
        bSky            = false;
        skyYPlane       = 0.0f;
        @skyactor1      = null;
    }
    
    ~TurokArea() {
    }
    
    //
    // TurokArea::OnThink
    //
    
    void OnThink(void) {
    }
    
    //
    // TurokArea::OnLocalThink
    //
    
    void OnLocalThink(void) {
        switch(ambienceType) {
            case 1:
                ambJungle.Update();
                break;
            case 2:
                ambSwamp.Update();
                break;
            case 3:
                ambCave.Update();
                break;
            case 4:
                ambCatcomb01.Update();
                break;
            case 5:
                ambLostWorld.Update();
                break;
            case 6:
                ambCatcomb02.Update();
                break;
            case 7:
                ambCatcomb01.Update();
                break;
            case 8:
                ambRuins.Update();
                break;
            case 9:
                ambValley.Update();
                break;
            case 10:
                ambVillage.Update();
                break;
            default:
                break;
        }
    }
    
    //
    // TurokArea::OnSpawn
    //
    
    void OnSpawn(void) {
        self.key.GetInt("ambience", ambienceType);
        self.key.GetBool("bSky", bSky, false);
        self.key.GetFloat("skyYPlane", skyYPlane);
    }
    
    //
    // TurokArea::OnEnter
    //
    
    void OnEnter(void) {
        LocalWorld.SetFogRGB(self.fogRGB.x, self.fogRGB.y, self.fogRGB.z);
        LocalWorld.SetFogFar(self.fogZFar);
        LocalWorld.SetFogNear(self.fogZFar * 0.5f);
        
        if(bSky == true) {
            if(skyactor1 is null) {
                @skyactor1 = LocalWorld.GetActorByName("levelsky1");
                
                if(skyactor1 is null) {
                    bSky = false;
                    return;
                }
            }
            
            cast<TurokSky@>(skyactor1.obj).height = skyYPlane;
            skyactor1.bHidden = false;
        }
    }
    
    //
    // TurokArea::OnExit
    //
    
    void OnExit(void) {
        if(!(skyactor1 is null)) {
            skyactor1.bHidden = true;
        }
    }
};
