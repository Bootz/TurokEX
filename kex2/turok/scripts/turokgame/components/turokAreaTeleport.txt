#include "scripts/turokgame/components/turokArea.txt"

////////////////////////////////////////////////////
//
// TurokAreaTeleport
//
////////////////////////////////////////////////////

final class TurokAreaTeleport : TurokArea {
    kVec3   position;
    float   facingYaw;
    int     mapID;
    int     sectorNum;
    bool    bEntered;
    
    //
    // TurokAreaTeleport::TurokAreaTeleport
    //
    
    TurokAreaTeleport(kArea @area) {
        super(area);
        bEntered = false;
    }
    
    ~TurokAreaTeleport() {
    }
    
    //
    // TurokAreaTeleport::OnThink
    //
    
    void OnThink(void) {
    }
    
    //
    // TurokAreaTeleport::OnLocalThink
    //
    
    void OnLocalThink(void) {
        // TODO: THIS SHOULD BE HANDLED BY SERVER
        if(bEntered == true && localPlayer.bDidWarped == false) {
            if(localClient.Puppet().Physics().OnGround()) {
                CameraEventTeleport event;
                
                turokGame.SetOverlayRGB(255, 255, 255);
                turokGame.SetOverlayAlpha(0);
                
                @event.teleport = this;
                event.StartEvent();
                @turokGame.camEvent = event;
                
                bEntered = false;
                localPlayer.bDidWarped = true;
            }
        }
        TurokArea::OnLocalThink();
    }
    
    //
    // TurokAreaTeleport::OnSpawn
    //
    
    void OnSpawn(void) {
        self.key.GetVector("position", position);
        self.key.GetFloat("facingYaw", facingYaw);
        self.key.GetInt("mapID", mapID);
        self.key.GetInt("sectorNum", sectorNum);
        
        TurokArea::OnSpawn();
    }
    
    //
    // TurokAreaTeleport::OnEnter
    //
    
    void OnEnter(void) {
        TurokArea::OnEnter();
        bEntered = true;
    }
    
    //
    // TurokAreaTeleport::OnExit
    //
    
    void OnExit(void) {
        TurokArea::OnExit();
        localPlayer.bDidWarped = false;
    }
};
