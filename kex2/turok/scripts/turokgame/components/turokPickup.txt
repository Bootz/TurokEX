////////////////////////////////////////////////////
//
// Enums
//
////////////////////////////////////////////////////

enum turokPickupTypes { 
    pt_smallhealth = 0,
    pt_mediumhealth,
    pt_largehealth,
    pt_fullhealth,
    pt_ultrahealth,
    pt_mask,
    pt_backpack,
    pt_spirit,
    pt_pistol = 9,
    pt_assaultrifle,
    pt_shotgun,
    pt_ashotgun,
    pt_minigun,
    pt_grenadelauncher,
    pt_pulserifle,
    pt_alienweapon,
    pt_rocketlauncher,
    pt_accelerator,
    pt_cannon,
    pt_quiver2,
    pt_arrows,
    pt_quiver1,
    pt_clip,
    pt_clipbox,
    pt_shells,
    pt_shellbox,
    pt_expshells,
    pt_expshellbox,
    pt_minigunammo,
    pt_grenade,
    pt_grenadebox,
    pt_smallcell,
    pt_cell,
    pt_rocket,
    pt_fusioncell = 36,
    pt_armor,
    pt_coin1,
    pt_coin10,
    pt_key1,
    pt_key2,
    pt_key3,
    pt_key4,
    pt_key5,
    pt_key6,
    pt_finalkey1,
    pt_chronopiece1,
    pt_finalkey2 = 49,
    pt_chronopiece2,
    pt_chronopiece3,
    pt_chronopiece4,
    pt_chronopiece5,
    pt_chronopiece6,
    pt_chronopiece7,
    pt_chronopiece8
}

////////////////////////////////////////////////////
//
// TurokPickup
//
////////////////////////////////////////////////////

class TurokPickup : Component {
    kActor @self;
    int type;
    
    //
    // TurokPickup::TurokPickup
    //
    
    TurokPickup(kActor @actor) {
        @self = actor;
        type = -1;
    }
    
    //
    // TurokPickup::OnThink
    //
    
    void OnThink(void) {
    }
    
    //
    // TurokPickup::OnLocalThink
    //
    
    void OnLocalThink(void) {
    }
    
    //
    // TurokPickup::OnSpawn
    //
    
    void OnSpawn(void) {
        self.Physics().bRotor = true;
        self.Physics().rotorSpeed = 1.0f;
        self.Physics().rotorFriction = 0;
        self.Physics().rotorVector = kVec3(0, 1, 0);
        
        self.args.GetInt("pickupType", type);
    }
    
    //
    // TurokPickup::OnTouch
    //
    
    bool OnTouch(kActor @instigator) {
        TurokPlayer @player;

        if(instigator is null) {
            return false;
        }
        
        kActor @owner = instigator.GetOwner();
        
        if(owner is null) {
            return false;
        }
        
        @player = cast<TurokPlayer@>(owner.obj);
        
        if(player is null) {
            return false;
        }
        
        switch(type) {
            case pt_smallhealth:
                if(!player.GiveHealth(2, false)) {
                    return false;
                }
                playerHud.Notify("2 health");
                break;
            case pt_mediumhealth:
                if(!player.GiveHealth(10, true)) {
                    return false;
                }
                playerHud.Notify("10 health");
                break;
            case pt_largehealth:
                if(!player.GiveHealth(25, true)) {
                    return false;
                }
                playerHud.Notify("25 health");
                break;
            case pt_fullhealth:
                player.GiveHealth(-1, false);
                playerHud.Notify("full health");
                break;
            case pt_ultrahealth:
                if(!player.GiveHealth(100, false)) {
                    return false;
                }
                playerHud.Notify("ultra health");
                break;
            case pt_coin1:
                player.GiveLifeForce(1);
                playerHud.lfSlider.Invoke();
                break;
            case pt_coin10:
                player.GiveLifeForce(10);
                playerHud.lfSlider.Invoke();
                break;
            case pt_clip:
                if(!player.clips.Give(player, 10)) {
                    return false;
                }
                playerHud.Notify("clip");
                break;
            case pt_clipbox:
                if(!player.clips.Give(player, 50)) {
                    return false;
                }
                playerHud.Notify("box of bullets");
                break;
            case pt_shells:
                if(!player.shells.Give(player, 5)) {
                    return false;
                }
                playerHud.Notify("shotgun shells");
                break;
            case pt_shellbox:
                if(!player.shells.Give(player, 20)) {
                    return false;
                }
                playerHud.Notify("box of shotgun shells");
                break;
            default:
                Com.Warning("Unknown pickup type: " + type);
                return true;
        }
        
        playerHud.Flash(0, 44, 148);
        return true;
    }
    
    //
    // TurokPickup::OnDamage
    //
    
    void OnDamage(void) {
    }
    
    //
    // TurokPickup::OnTrigger
    //
    
    void OnTrigger(void) {
    }
};
