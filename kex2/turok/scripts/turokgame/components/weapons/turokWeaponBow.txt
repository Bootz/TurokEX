#include "scripts/turokgame/components/weapons/turokWeapon.txt"

////////////////////////////////////////////////////
//
// TurokWeaponBow
//
////////////////////////////////////////////////////

final class TurokWeaponBow : TurokWeapon {
    bool bAiming;
    
    //
    // TurokWeaponBow::TurokWeaponBow
    //
    
    TurokWeaponBow(kActor @actor) {
        super(actor);
        bAiming = false;
        bHasAltAmmo = true;
        bOwned = true;
    }
    
    ~TurokWeaponBow() {
    }
    
    //
    // TurokWeaponBow::OnAttack
    //
    
    bool OnAttack(void) {
        if(bAiming == true) {
            return true;
        }
        
        owner.self.Puppet().StartSound("sounds/shaders/bow_stretch.ksnd");
        self.AnimState().Blend(anim_weaponFire, playSpeed, 18.0f, ANF_LOOP);
        
        bAiming = true;
        return true;
    }
    
    //
    // TurokWeaponBow::CheckAttack
    //
    
    void CheckAttack(void) {
        if(bAiming) {
            if(owner.self.ActionHeldTime("+attack") > 0) {
                return;
            }
            
            float time  = self.AnimState().PlayTime();
            kActor @src = owner.self.Puppet();
            kQuat rot   = src.GetRotation();
            kVec3 org   = src.GetOrigin();
        
            org.y += 51.2f;
            
            if(time > 0.4f) {
                time = 0.4f;
            }
            
            owner.arrows.Consume(1);
            owner.self.Puppet().StartSound("sounds/shaders/bow_twang.ksnd");
            bAiming = false;
            
            LocalWorld.SpawnFX(
                "fx/projectile_arrow.kfx",
                src,
                kVec3(0, 0, time * 512 * 15) * rot,
                org + (kVec3(0.2048f, -6.144f, -25.6f) * rot),
                rot
            );
            
            self.AnimState().Blend(anim_weaponFireCharged, playSpeed, 4.0f, 0);
        }
        
        TurokWeapon::CheckAttack();
    }
    
    //
    // TurokWeaponBow::OnSpawn
    //
    
    void OnSpawn(void) {
        xOffset = 133.12026f;
        yOffset = -12.62882f;
        zOffset = -150.18696f;
        self.SetModel("models/mdl644/mdl644.kmesh");
        
        TurokWeapon::OnSpawn();
    }
};
